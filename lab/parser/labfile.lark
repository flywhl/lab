// Labfile Grammar

start: statement+

statement: provider
         | experiment
         | plot

provider: "PROVIDER" simple_identifier

experiment: "EXPERIMENT" simple_identifier "AS" simple_identifier for_clause? repeat_clause? with_clause

plot: "PLOT" dotted_identifier

for_clause: "FOR" simple_identifier "IN" (list | dotted_identifier)

repeat_clause: "REPEAT" (NUMBER | "all")

with_clause: "WITH" with_item+

with_item: simple_identifier "FROM" dotted_identifier
         | simple_identifier simple_identifier
         | "AND" simple_identifier (simple_identifier | "FROM" dotted_identifier)
         | "AND" simple_identifier "WITH" hyper_param+

hyper_param: simple_identifier (NUMBER | "FROM" dotted_identifier)

dotted_identifier: simple_identifier ("." simple_identifier)*

list: "[" [simple_identifier ("," simple_identifier)*] "]"

simple_identifier: /[a-zA-Z_][\w-]*/

%import common.NUMBER
%import common.WS
%ignore WS
